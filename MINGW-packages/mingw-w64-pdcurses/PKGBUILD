# Usage examples: makepkg MINGW_PREFIX=/mingw64 CARCH=x86_64
#                 makepkg MINGW_PREFIX=/mingw32 CARCH=i686

_realname=pdcurses
_mingw_suff=mingw-w64-${CARCH}
pkgname="${_mingw_suff}-${_realname}"
pkgver=3.4.0
pkgrel=1
pkgdesc="A public domain curses library for DOS, OS/2, Win32, X11 and SDL, implementing most of the functions available in X/Open and System V R4 curses."
arch=('any')
url="http://pdcurses.sourceforge.net"
license=("Public domain")
makedepends=("${_mingw_suff}-gcc")
depends=("${_mingw_suff}-gcc-libs")
options=('staticlibs' 'strip')
source=("http://sourceforge.net/projects/pdcurses/files/pdcurses/3.4/PDCurses-3.4.tar.gz")
md5sums=('4e04e4412d1b1392a7f9a489b95b331a')

prepare() {
  cd "${srcdir}/PDCurses-3.4/win32"
  patch -p0 < ../../../mingwin32.mak.patch
}


build() {
  cd "${srcdir}/PDCurses-3.4/win32"
  
  rm -rf {include,bin,lib}
  mkdir -p {include,bin,lib}

  make -f mingwin32.mak clean
  make -f mingwin32.mak DLL=Y WIDE=Y UTF8=Y libpdcursesu.dll
  
  cp ../curses.h include/cursesu.h
  patch -d include < ../../../cursesu.h.patch
  cp libpdcursesu.dll bin/
  cp libpdcursesu.dll.a lib/

  make -f mingwin32.mak clean
  make -f mingwin32.mak DLL=Y WIDE=Y libpdcursesw.dll
  
  cp ../curses.h include/cursesw.h
  patch -d include < ../../../cursesw.h.patch
  cp libpdcursesw.dll bin/
  cp libpdcursesw.dll.a lib/

  make -f mingwin32.mak clean
  make -f mingwin32.mak DLL=Y libpdcurses.dll
  
  cp ../curses.h include/curses.h
  patch -d include < ../../../curses.h.patch
  cp libpdcurses.dll bin/
  cp libpdcurses.dll.a lib/
}

package() {
  cd "${srcdir}/PDCurses-3.4/win32"
  mkdir -p "${pkgdir}/${MINGW_PREFIX}"/{include,bin,lib}
  for folder in {include,bin,lib}
  do
    install -m 0644 $folder/* "${pkgdir}/${MINGW_PREFIX}"/$folder/
  done
}
